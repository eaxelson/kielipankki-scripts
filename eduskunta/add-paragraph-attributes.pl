#!/usr/bin/perl

# Read vrt format from standard input and extract additional
# attributes from participant attribute of each paragraph element
# and append them as new attributes to the given paragraph element
# and remove extracted data from value of participant. If an
# attribute cannot be extracted, give it an empty value "".
# Attributes extracted from participant are:
#
# * group: parliamentary group, e.g. "kesk"
# * type: speech type, e.g. "vastauspuheenvuoro"
# * role: speaker role, e.g. "puhemies"

use strict;
use warnings;
use open qw(:std :utf8);

foreach my $line ( <STDIN> ) {

    # process only paragraph elements
    if ( $line =~ /^<paragraph / )
    {
	# exit if participant is not given
	unless ( $line =~ /participant="/ )
	{
	    print STDERR "Error: no participant given for paragraph: ";
	    print STDERR $line;
	    print STDERR "\n";
	    exit 1;
	}

	# get rid of extra space and use only ordinary space
	$line =~ s/\h/ /g;
	$line =~ s/ +/ /g;
	$line =~ s/ "/"/g;

	# extract value of participant and modify it to get additional attributes
	my $participant = $line;
	$participant =~ s/.*participant="([^"]+)".*/$1/;

	# group given after slash, e.g. "/kesk", "/kok", "/sd" etc.
	my $group = $participant;
	if ( $group =~ /\// )
	{
	    $group =~ s/.*\/([a-z\x{00E4}\x{00F6}A-Z\x{00C4}\x{00D6}0-9]*).*/$1/;
	    $group =~ s/^\s+|\s+$//g;
	    # remove group from participant
	    $line =~ s/\s\/[a-z\x{00E4}\x{00F6}A-Z\x{00C4}\x{00D6}0-9]*//;
	}
	# group given inside first parentheses, e.g. "(vas)", "(kd)", "(ps)" etc.
	elsif ( $group =~ /\([^\)]/ )
	{
	    $group =~ s/[^\(]*\(([^\)]+)\).*/$1/;
	    $group =~ s/^\s+|\s+$//g;

	    # "(koputtaa)" is speech type, not a group
	    if ( $group eq "koputtaa" )
	    {
		$group = "";
	    }
	    else
	    {
		# remove group from participant
		$line =~ s/\s?\([^\)]+\)//;
	    }
	}
	# no group given
	else
	{
	    $group = "";
	}

	# append group attribute
	if( $group =~ /^(kd|kesk|kok|m11|ps|r|sd|vas|vihr|vr)$/ || $group eq "" )
	{
	    $line =~ s/>/ speaker_parl_group="$group">/;
	}
	else
	{
	    print STDERR "Warning: parliamentary group not recognized, setting it to empty value: \"";
	    print STDERR $group;
	    print STDERR "\"\n";
	    $line =~ s/>/ speaker_parl_group="">/;
	}

	# remove content in parentheses in participant
	$line =~ s/(participant="[^"\(]+)(\([^"\)]+\))/$1/g;
	# and append it to attribute 'type'
	if ( defined $2 )
	{
	    my $original_type = $2;
	    my $type = $original_type;
	    $type =~ s/\((.*)\)/$1/;
	    $type =~ s/Vastauspuheenvuoro/vastauspuheenvuoro/;
	    $type =~ s/\x{00E4}/a/g;

	    if ( $type eq "vastauspuheenvuoro" ||
		 $type eq "ryhmapuheenvuoro" ||
		 $type eq "esittelypuheenvuoro" ||
		 $type eq "koputtaa" )
	    {
		$line =~ s/>/ speech_type="$type">/;
	    }
	    else
	    {
		print STDERR "Warning: speech type not recognized, setting it to empty value: \"";
		print STDERR $original_type;
		print STDERR "\"\n";
		$line =~ s/>/ speech_type="">/;
	    }
	}
	else
	{
	    $line =~ s/>/ speech_type="">/;
	}

	# exctract possible role in participant
	# and move it to attribute 'role'
	my $possible_role = $participant;
	if ( $possible_role =~ /[A-Z\x{00C4}\x{00D6}][a-z\x{00E4}\x{00F6}]+(\- ja [a-z\x{00E4}\x{00F6}]+)?ministeri/ ||
	     $possible_role =~ /Eduskunnan oikeusasiamies/ ||
	     $possible_role =~ /Ensimm\x{00E4}inen puhemies/ ||
	     $possible_role =~ /Ensimm\x{00E4}inen varapuhemies/ ||
	     $possible_role =~ /Ik\x{00E4}puhemies/ ||
	     $possible_role =~ /Puhemies/ ||
	     $possible_role =~ /Puhuja/ ||
	     $possible_role =~ /Toinen varapuhemies/ ||
	     $possible_role =~ /Valtioneuvoston apulaisoikeuskansleri/ ||
	     $possible_role =~ /Valtioneuvoston oikeuskansleri/ ||
	     $possible_role =~ /varaj\x{00E4}senen\x{00E4}\x{00E4}n( kansanedustaja)?/ )
	{
	    if ( defined $& )
	    {
		my $role = $&;
		my $orig_role = $role;
		$role = lc $role;
		$role =~ s/\-//g;
		$role =~ s/ /_/g;
		$role =~ s/\x{00E4}/a/g;
		$role =~ s/\x{00F6}/o/g;
		$role =~ s/^ensimmainen_puhemies$/puhemies/g;
		$role =~ s/^varajasenenaan(_kansanedustaja)?$/varajasen/g;
		$line =~ s/>/ speaker_role="$role">/;
		$line =~ s/participant="${orig_role}/participant="/g;
	    }
	}
	else
	{
	    $line =~ s/>/ speaker_role="">/;
	}

	# remove possible extra space generated by role extraction
	$line =~ s/=" /="/g;

	# check for strange characters
	$participant = $line;
	$participant =~ s/.*participant="([^"]*)".*/$1/;
	$participant =~ s/\n//g;
	# participant must be empty string or UNKNOWN or consist of "a-åäöA-ÅÄÖé .-" and contain one or two spaces
	# and not contain a word starting with lowercase character
	unless ( $participant eq "" || $participant eq "UNKNOWN" ||
		 ( $participant =~ /^[a-z\x{00E5}\x{00E4}\x{00F6}\x{00E9}A-Z\x{00C5}\x{00C4}\x{00D6} \.\-]+$/ &&
		   $participant =~ / / &&
		   $participant !~ / [^ ]+ [^ ]+ / &&
		   $participant !~ / [a-z\x{00E5}\x{00E4}\x{00F6}\x{00E9}]/ &&
		   $participant !~ /^[a-z\x{00E5}\x{00E4}\x{00F6}\x{00E9}]/ ) )
	{
	    print STDERR "Warning: suspicious participant name, setting it to empty value: \"";
	    print STDERR $participant;
	    print STDERR "\"\n";
	    $line =~ s/(.*participant=")[^"]+(".*)/$1$2/;
	}
    }

    print $line;
}

